buildscript {
    ext.kotlinVersion = '1.9.23'

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.4.0"
    }
}

ext {
  //dependency version
  moengageCoreVersion = "13.05.02"
  moengageCardsCoreVersion = "2.3.2"
  pluginBaseCardVersion = "3.0.1"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'com.facebook.react'

android {
    compileSdk 34
    namespace "com.moengage.react.cards"
    defaultConfig {
        minSdk 21
        versionCode 1
        versionName "1.0"

        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=all']
    }
    buildFeatures {
        buildConfig true
    }
    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch']
            } else {
                java.srcDirs += ['src/oldarch']
            }
        }
    }
}

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
}

dependencies {
    // noinspection GradleDynamicVersion
    compileOnly('com.facebook.react:react-native')
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compileOnly("com.moengage:moe-android-sdk:$moengageCoreVersion")
    api("com.moengage:cards-core:$moengageCardsCoreVersion")
    implementation("com.moengage:plugin-base-cards:$pluginBaseCardVersion")
}

def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}